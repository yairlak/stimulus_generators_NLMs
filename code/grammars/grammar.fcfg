########
# SVOs #
########

S[GROUP=?e] -> subjNP[NUM=?n, PERS=?p, ANIM=?a, GROUP=?e] VP[NUM=?n, PERS=?p, ANIM=?a]

# subject
subjNP[NUM=?n, PERS=?p, GEN=?g, ANIM=?a, GROUP=svo] -> subj_PropN[NUM=?n, GEN=?g, PERS=?p, ANIM=?a]
subjNP[NUM=?n, PERS=?p, GEN=?g, ANIM=?a, GROUP=svo] -> Det subj_N[NUM=?n, GEN=?g, PERS=?p, ANIM=?a]
subjNP[NUM=?n, PERS=?p, GEN=?g, ANIM=?a, GROUP=svo] -> subj_Quantifier[NUM=?n, PERS=?p] subj_N[NUM=?n, GEN=?g, PERS=?p, ANIM=?a]
subjNP[NUM=?n, PERS=?p, GEN=?g, ANIM=?a, GROUP=svo] -> subj_PRO[NUM=?n, GEN=?g, PERS=?p, ANIM=?a]

# verb
VP[NUM=?n, PERS=?p, ANIM=?a] -> verb_Intrans[finite=true, NUM=?n, PERS=?p, ANIM=?a]
VP[NUM=?n, PERS=?p, ANIM=?a] -> verb_Trans[finite=true, NUM=?n, PERS=?p, ANIM=?a] objNP

# object
objNP -> obj_PropN
objNP -> Det obj_N
objNP -> obj_Quantifier[NUM=?n] obj_N[NUM=?n]
objNP -> obj_PRO[BOUND=false]

#############
# QUESTIONS #
#############

# subject questions (intransitive verb)
S[GROUP=?e] -> subjWho[GROUP=?e] verb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=3, ANIM=true]
subjWho[GROUP=subjquest_intrans] -> subj_who

# subject questions (transitive verb)
S[GROUP=?e] -> subjWH[GROUP=?e]
subjWH[GROUP=subjquest_trans] -> subj_who verb_Trans[finite=true, NUM=sg, PERS=3, ANIM=true] Det obj_N
subjWH[GROUP=subjquest_trans] -> subj_which subj_N[NUM=?n, GEN=?g, PERS=?p, ANIM=?a] verb_Trans[finite=true, NUM=?n, PERS=?p, ANIM=?a] Det obj_N

# object questions
S[GROUP=?e] -> objWh[GROUP=?e] verb_Trans[finite=false, ANIM=true]
objWh[GROUP=objquest_trans, NUM=?n, PERS=?p] -> obj_WH do_Aux[NUM=?n, PERS=?p] Det subj_N[NUM=?n, PERS=?p, ANIM=true]

# TODO: which NP questions?

########################
# Long-Range Agreement #
########################
S[GROUP=?e] -> Det subj_N[NUM=?n, ANIM=?a] nestedPP[GROUP=?e] verb_Intrans[TENSE=pres, NUM=?n, ANIM=?a]
nestedPP[GROUP=pp] -> P Det embedsubj_N[GROUP=pp]

##############
# EMBEDDINGS #
##############
# object RCs
S[GROUP=?g] -> Det subj_N[NUM=?n, PERS=3, ANIM=true] rel_That Objrel[GROUP=?g] verb_Intrans[finite=true, TENSE=pres, NUM=?n, PERS=3, ANIM=true]
Objrel[GROUP=objrel] -> Det embedsubj_N[NUM=?n, ANIM=true, GROUP=objrel] embedverb_Trans[finite=true, NUM=?n, TENSE=pres, PERS=3]

# subject RCs
S[GROUP=?g] -> Det subj_N[NUM=?n, PERS=3, ANIM=true] rel_That Subjrel[NUM=?n, PERS=3, ANIM=true, GROUP=?g] verb_Intrans[finite=true, TENSE=pres, NUM=?n, PERS=3, ANIM=true]
Subjrel[NUM=?n, PERS=3, ANIM=true, GROUP=subjrel] -> embedverb_Trans[finite=true, NUM=?n, TENSE=pres, PERS=3] Det embedsubj_N[NUM=?n, ANIM=true, GROUP=subjrel]

# Embedding
S[GROUP=?g] -> Det subj_N[NUM=?n, PERS=?p, ANIM=true] verb_Matrix[finite=true, NUM=?n, PERS=?p, ANIM=true] nestedClause[GROUP=?g]
nestedClause[GROUP=that_clause] -> rel_That Det embedsubj_N[NUM=?n, GROUP=embed_clause] embedverb_Intrans[finite=true, NUM=?n, PERS=3]
nestedClause[GROUP=whether_clause] -> rel_Whether Det embedsubj_N[NUM=?n, GROUP=embed_clause] embedverb_Intrans[finite=true, NUM=?n, PERS=3]
nestedClause[GROUP=subjwho_clause] -> subj_who verb_Trans[finite=true, NUM=sg, PERS=3, ANIM=true] Det obj_N
nestedClause[GROUP=objwho_clause] -> nestedobjWh[GROUP=?e] verb_Trans[finite=true, NUM=?n, PERS=?p, ANIM=true]
nestedobjWh[GROUP=objwho_clause, NUM=?n, PERS=?p] -> obj_WH Det subj_N[NUM=?n, PERS=?p, ANIM=true]
nestedClause -> Rel[REL=which] embedsubj_N[NUM=?n] embedverb_Intrans[finite=true, NUM=?n, PERS=3]

#TODO: embedded which NP clauses?

###########
# BINDING #
###########

# Binding (reflexives)
S[GROUP=?e, NUM=?n, GEN=?g, PERS=?p, ANIM=?a] -> subjNP[NUM=?n, GEN=?g, PERS=?p, ANIM=?a] verb_Trans[finite=true, TENSE=?t, NUM=?n, PERS=?p, ANIM=?a] objPRO[GROUP=?e, NUM=?n, GEN=?g, PERS=?p, ANIM=?a]
objPRO[GROUP=binding, NUM=?n, GEN=?g, PERS=?p, ANIM=?a] -> obj_PRO[NUM=?n, GEN=?g, PERS=?p, ANIM=?a, BOUND=true]

# Binding (possessives)
S[GROUP=?e, NUM=?n, GEN=?g, PERS=?p, ANIM=?a] -> subjNP[NUM=?n, GEN=?g, PERS=?p, ANIM=?a] verb_Trans[finite=true, TENSE=?t, NUM=?n, PERS=?p, ANIM=?a] possNP[GROUP=?e]
S[GROUP=?e, NUM=?n, GEN=?g, PERS=?p, ANIM=?a] -> possNP[GROUP=?e, NUM=?n, GEN=?g, PERS=?p, ANIM=?a] verb_Trans[finite=true, TENSE=?t, NUM=?n, PERS=?p, ANIM=?a] objNP[NUM=?n, GEN=?g, PERS=?p, ANIM=?a]
possNP[GROUP=possessive, NUM=?n, GEN=?g, PERS=?p, ANIM=?a, possNUM=?pn, possGEN=?pg, possPERS=?pp, POSS=true] -> poss_Det[possNUM=?pn, possGEN=?pg, possPERS=?pp, POSS=true, BOUND=TESTFEATUREMATCH] possN[POSS=true, NUM=?n, GEN=?g, ANIM=?a, PERS=?p]

#######################
# Lexical Productions #
#######################

# subject
subj_Det[NUM=sg] -> 'this'
subj_Det[NUM=pl] -> 'these'
subj_Det -> 'the'

subj_Quantifier[NUM=sg] -> 'every'
subj_Quantifier[NUM=pl] -> 'all'

subj_PRO[NUM=sg, PERS=1, ANIM=true]->'I'
subj_PRO[NUM=pl, PERS=2, ANIM=true]->'you'
subj_PRO[NUM=sg, PERS=3, GEN=m, ANIM=true]->'he'
subj_PRO[NUM=sg, PERS=3, GEN=f, ANIM=true]->'she'
subj_PRO[NUM=sg, PERS=3, ANIM=false]->'it'
subj_PRO[NUM=pl, PERS=3, ANIM=true]->'they'
subj_PRO[NUM=pl, PERS=1, ANIM=true]->'we'

subj_who[ANIM=true] -> 'who'
subj_which -> 'which'

subj_N[NUM=sg, GEN=m, PERS=3, ANIM=true] -> 'boy'
subj_N[NUM=sg, GEN=f, PERS=3, ANIM=true] -> 'girl'
subj_N[NUM=pl, GEN=m, PERS=3, ANIM=true] -> 'boys'
subj_N[NUM=pl, GEN=f, PERS=3, ANIM=true] -> 'girls'
subj_N[NUM=sg, PERS=3, ANIM=false] -> 'car'
subj_N[NUM=pl, PERS=3, ANIM=false] -> 'cars'

subj_PropN[NUM=sg, GEN=f, PERS=3, ANIM=true]-> 'Alice'
subj_PropN[NUM=sg, GEN=m, PERS=3, ANIM=true]-> 'Bob'

embedsubj_N[NUM=sg, GEN=m, PERS=3, ANIM=true] -> 'boy'
embedsubj_N[NUM=sg, GEN=f, PERS=3, ANIM=true] -> 'girl'
embedsubj_N[NUM=pl, GEN=m, PERS=3, ANIM=true] -> 'boys'
embedsubj_N[NUM=pl, GEN=f, PERS=3, ANIM=true] -> 'girls'

# object
obj_Det[NUM=sg] -> 'this'
obj_Det[NUM=pl] -> 'these'
obj_Det -> 'the'

obj_Quantifier[NUM=sg] -> 'every'
obj_Quantifier[NUM=pl] -> 'all'

obj_PRO[NUM=sg, PERS=1, ANIM=true, BOUND=false]->'me'
obj_PRO[NUM=sg, PERS=1, ANIM=true, BOUND=true]->'myself'
obj_PRO[PERS=2, ANIM=true, BOUND=false]->'you'
obj_PRO[PERS=2, NUM=sg, ANIM=true, BOUND=true]->'yourself'
obj_PRO[PERS=2, NUM=pl, ANIM=true, BOUND=true]->'yourselves'
obj_PRO[NUM=sg, GEN=m, PERS=3, ANIM=true, BOUND=false]->'him'
obj_PRO[NUM=sg, GEN=m, PERS=3, ANIM=true, BOUND=true]->'himself'
obj_PRO[NUM=sg, GEN=f, PERS=3, ANIM=true, BOUND=false]->'her'
obj_PRO[NUM=sg, GEN=f, PERS=3, ANIM=true, BOUND=true]->'herself'
obj_PRO[NUM=sg, GEN=m, PERS=3, ANIM=false, BOUND=false]->'it'
obj_PRO[NUM=sg, GEN=m, PERS=3, ANIM=false, BOUND=true]->'itself'
obj_PRO[NUM=pl, PERS=3, ANIM=true, BOUND=false]->'them'
obj_PRO[NUM=pl, PERS=3, ANIM=true, BOUND=true]->'themselves'
obj_PRO[NUM=pl, PERS=1, ANIM=true, BOUND=false]->'us'
obj_PRO[NUM=pl, PERS=1, ANIM=true, BOUND=true]->'ourselves'

obj_N[NUM=sg, GEN=m, PERS=3, ANIM=true] -> 'boy'
obj_N[NUM=sg, GEN=f, PERS=3, ANIM=true] -> 'girl'
obj_N[NUM=pl, GEN=m, PERS=3, ANIM=true] -> 'boys'
obj_N[NUM=pl, GEN=f, PERS=3, ANIM=true] -> 'girls'

obj_N[NUM=sg, PERS=3, ANIM=false] -> 'car'
obj_N[NUM=pl, PERS=3, ANIM=false] -> 'cars'

obj_WH -> 'who'|'whom'

obj_PropN[NUM=sg, GEN=f, PERS=3, ANIM=true]-> 'Alice'
obj_PropN[NUM=sg, GEN=m, PERS=3, ANIM=true]-> 'Bob'

embedobj_N[NUM=sg, GEN=m, PERS=3, ANIM=true] -> 'boy'
embedobj_N[NUM=sg, GEN=f, PERS=3, ANIM=true] -> 'girl'
embedobj_N[NUM=pl, GEN=m, PERS=3, ANIM=true] -> 'boys'
embedobj_N[NUM=pl, GEN=f, PERS=3, ANIM=true] -> 'girls'

# verb
verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=1, ANIM=true] -> 'walk'
verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=2, ANIM=true] -> 'walk'
verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=3, ANIM=true] -> 'walks'
verb_Intrans[finite=true, TENSE=pres,  NUM=pl, ANIM=true] -> 'walk'
verb_Intrans[finite=true, TENSE=past, ANIM=true] -> 'walked'
verb_Intrans[finite=true, TENSE=future, ANIM=true] -> 'will walk'

verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=1] -> 'fall'
verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=2] -> 'fall'
verb_Intrans[finite=true, TENSE=pres,  NUM=sg, PERS=3] -> 'falls'
verb_Intrans[finite=true, TENSE=pres,  NUM=pl] -> 'fall'
verb_Intrans[finite=true, TENSE=past] -> 'fell'
verb_Intrans[finite=true, TENSE=future] -> 'will fall'

embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=1, ANIM=true] -> 'walk'
embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=2, ANIM=true] -> 'walk'
embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=3, ANIM=true] -> 'walks'
embedverb_Intrans[finite=true, TENSE=pres, NUM=pl, ANIM=true] -> 'walk'
embedverb_Intrans[finite=true, TENSE=past, ANIM=true] -> 'walked'
embedverb_Intrans[TENSE=future] -> 'will walk'
embedverb_Intrans[finite=false] -> 'walk'

embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=1] -> 'fall'
embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=2] -> 'fall'
embedverb_Intrans[finite=true, TENSE=pres, NUM=sg, PERS=3] -> 'falls'
embedverb_Intrans[finite=true, TENSE=pres, NUM=pl] -> 'fall'
embedverb_Intrans[finite=true, TENSE=past] -> 'fell'
embedverb_Intrans[finite=true, TENSE=future] -> 'will fall'
embedverb_Intrans[finite=false] -> 'fall'

verb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=1, ANIM=true] -> 'see'
verb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=2, ANIM=true] -> 'see'
verb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=3, ANIM=true] -> 'sees'
verb_Trans[finite=true, TENSE=pres, NUM=pl, ANIM=true] -> 'see'
verb_Trans[finite=true, TENSE=past, ANIM=true] -> 'saw'
verb_Trans[finite=true, TENSE=future, ANIM=true] -> 'will see'
verb_Trans[finite=false, ANIM=true] -> 'see'

embedverb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=1, ANIM=true] -> 'see'
embedverb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=2, ANIM=true] -> 'see'
embedverb_Trans[finite=true, TENSE=pres, NUM=sg, PERS=3, ANIM=true] -> 'sees'
embedverb_Trans[finite=true, TENSE=pres, NUM=pl, ANIM=true] -> 'see'
embedverb_Trans[finite=true, TENSE=past, ANIM=true] -> 'saw'
embedverb_Trans[finite=true, TENSE=future, ANIM=true] -> 'will see'
embedverb_Trans[finite=false, ANIM=true] -> 'see'


verb_Matrix[finite=true, TENSE=pres, NUM=sg, PERS=1, ANIM=true] -> 'know'
verb_Matrix[finite=true, TENSE=pres, NUM=sg, PERS=2, ANIM=true] -> 'know'
verb_Matrix[finite=true, TENSE=pres, NUM=sg, PERS=3, ANIM=true] -> 'knows'
verb_Matrix[finite=true, TENSE=pres, NUM=pl, PERS=3, ANIM=true] -> 'know'
verb_Matrix[finite=true, TENSE=past] -> 'knew'
verb_Matrix[finite=true, TENSE=future, ANIM=true] -> 'will know'
verb_Matrix[finite=false, ANIM=true] -> 'know'
# Other options which can embed which,whether,that-clauses: 'remember'|'say'|'declare'


# Other
do_Aux[TENSE=pres, NUM=sg, PERS=1] -> 'do'
do_Aux[TENSE=pres, NUM=sg, PERS=2] -> 'do'
do_Aux[TENSE=pres, NUM=sg, PERS=3] -> 'does'
do_Aux[TENSE=pres, NUM=pl] -> 'do'
do_Aux[TENSE=past] -> 'did'
do_Aux[TENSE=future, NUM=pl] -> 'will'

Det -> 'the'

V[ANIM=true] -> 'walk'
V -> 'fall'
P -> 'near'
rel_That -> 'that'
rel_Whether -> 'whether'
DetWh -> 'which'

poss_Det[NUM=sg, PERS=1, POSS=true, BOUND=TESTFEATUREMATCH] -> 'my'
poss_Det[NUM=pl, PERS=1, POSS=true, BOUND=TESTFEATUREMATCH] -> 'our'

poss_Det[PERS=2, POSS=true, BOUND=TESTFEATUREMATCH] -> 'your'

poss_Det[NUM=sg, GEN=f, PERS=3, POSS=true, BOUND=TESTFEATUREMATCH] -> 'her'
poss_Det[NUM=sg, GEN=m, PERS=3, POSS=true, BOUND=TESTFEATUREMATCH] -> 'his'
poss_Det[NUM=pl, GEN=m, PERS=3, POSS=true, BOUND=TESTFEATUREMATCH] -> 'their'
poss_Det[NUM=pl, GEN=f, PERS=3, POSS=true, BOUND=TESTFEATUREMATCH] -> 'their'

possN[NUM=sg, GEN=f, PERS=3, POSS=true, ANIM=true] -> 'mother'
possN[NUM=sg, GEN=m, PERS=3, POSS=true, ANIM=true] -> 'father'
possN[NUM=sg, GEN=f, PERS=3, POSS=true, ANIM=true] -> 'sister'
possN[NUM=sg, GEN=m, PERS=3, POSS=true, ANIM=true] -> 'brother'
possN[NUM=pl, GEN=f, PERS=3, POSS=true, ANIM=true] -> 'sisters'
possN[NUM=pl, GEN=m, PERS=3, POSS=true, ANIM=true] -> 'brothers'
possN[NUM=sg, GEN=f, PERS=3, POSS=true, ANIM=false] -> 'cat'
possN[NUM=sg, GEN=m, PERS=3, POSS=true, ANIM=false] -> 'dog'
possN[NUM=pl, GEN=f, PERS=3, POSS=true, ANIM=false] -> 'cats'
possN[NUM=pl, GEN=m, PERS=3, POSS=true, ANIM=false] -> 'dogs'
